plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.hanafey.android.wol'

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
    def ec = []
    for (n in ['upload-keystore','upload-keystore-pw','upload-key-alias','upload-key-pw']) {
        if (!localProperties.containsKey(n)) {
            logger.error("local.properties must define $n")
            ec.add(n)
        }
    }
    if (ec.size()> 0) {
        throw new GradleException("local.properties bundle build requires keys $ec.")
    }

    signingConfigs {
        debug_signing_config {
            storeFile file(localProperties['upload-keystore'])
            storePassword localProperties['upload-keystore-pw']
            keyAlias localProperties['upload-key-alias']
            keyPassword localProperties['upload-key-pw']
        }
        release_signing_config {
            storeFile file(localProperties['upload-keystore'])
            storePassword localProperties['upload-keystore-pw']
            keyAlias localProperties['upload-key-alias']
            keyPassword localProperties['upload-key-pw']
        }
    }
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.hanafey.android.wol"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 12
        versionName "2.0.2"

        // Set these LON_ vals to  false to disable debug logging.
        buildConfigField "boolean", "LON_HostStatusFragment", "false"
        buildConfigField "boolean", "LON_MainActivity", "false"
        buildConfigField "boolean", "LON_MainFragment", "false"
        buildConfigField "boolean", "LON_MainViewModel", "false"
        buildConfigField "boolean", "LON_MainViewModel_noisy", "false"
        buildConfigField "boolean", "LON_ObserverOfHostState", "false"
        buildConfigField "boolean", "LON_PingDeadToAwakeTransition", "false"
        buildConfigField "boolean", "LON_WolApplication", "false"
        buildConfigField "boolean", "LON_SettingsFragment", "false"
        buildConfigField "boolean", "LON_SettingsHostFragment", "false"
        buildConfigField "boolean", "LON_SettingHostListFragment", "false"
        buildConfigField "boolean", "LON_", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug_signing_config
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            signingConfig signingConfigs.release_signing_config
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    implementation 'com.hanafey.android.ax:ax:1.0.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation('androidx.lifecycle:lifecycle-livedata-ktx:2.5.1')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.webkit:webkit:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
